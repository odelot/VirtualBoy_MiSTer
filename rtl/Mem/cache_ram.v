// Copyright (c) 2026 Jamie Blanks

// Parameterized synchronous block RAMs. Quartus uses Altera primitives;
// other tools use the portable models below. Accesses take one clock.

module cache_ram
#(
	parameter ADDR_WIDTH = 7,
	parameter DATA_WIDTH = 32,
	/* verilator lint_off UNUSEDPARAM */
	parameter MEM_INIT_FILE = " ",
	/* verilator lint_on UNUSEDPARAM */
	parameter SIM_INIT_FILE = " ",
	/* verilator lint_off UNUSEDPARAM */
	parameter DEVICE_FAMILY = "Cyclone V"
	/* verilator lint_on UNUSEDPARAM */
)
(
	input  wire                  clk_i,
	input  wire [ADDR_WIDTH-1:0] addr_i,
	input  wire                  wren_i,
	input  wire [DATA_WIDTH-1:0] wdata_i,
	output wire [DATA_WIDTH-1:0] q_o
);
	localparam NUM_WORDS = (1 << ADDR_WIDTH);

	`ifdef ALTERA_RESERVED_QIS
	altsyncram #(
		.init_file                     (MEM_INIT_FILE),
		.intended_device_family        (DEVICE_FAMILY),
		.lpm_hint                      ("ENABLE_RUNTIME_MOD=NO"),
		.lpm_type                      ("altsyncram"),
		.numwords_a                    (NUM_WORDS),
		.operation_mode                ("SINGLE_PORT"),
		.outdata_aclr_a                ("NONE"),
		.outdata_reg_a                 ("UNREGISTERED"),
		.power_up_uninitialized        ("FALSE"),
		.ram_block_type                ("M10K"),
		.read_during_write_mode_port_a ("NEW_DATA_NO_NBE_READ"),
		.width_a                       (DATA_WIDTH),
		.width_byteena_a               (1),
		.widthad_a                     (ADDR_WIDTH)
	) u_ram (
		.address_a (addr_i),
		.clock0    (clk_i),
		.data_a    (wdata_i),
		.q_a       (q_o),
		.wren_a    (wren_i)
	);

`else
	reg [DATA_WIDTH-1:0] q_out;

	(* ramstyle = "M10K, no_rw_check" *) reg [DATA_WIDTH-1:0] mem_q [0:NUM_WORDS-1];

	initial begin
		if (SIM_INIT_FILE != " ") begin
			$readmemh(SIM_INIT_FILE, mem_q);
		end
	end

	always @(posedge clk_i) begin
		if (wren_i) begin
			mem_q[addr_i] <= wdata_i;
		end

		if (wren_i) begin
			q_out <= wdata_i;
		end else begin
			q_out <= mem_q[addr_i];
		end
	end

	assign q_o = q_out;
`endif

endmodule

module cache_ram_dp
#(
	parameter ADDR_WIDTH = 7,
	parameter DATA_WIDTH = 32,
	/* verilator lint_off UNUSEDPARAM */
	parameter MEM_INIT_FILE = " ",
	parameter DEVICE_FAMILY = "Cyclone V",
	/* verilator lint_on UNUSEDPARAM */
	parameter SIM_INIT_FILE = " "
)
(
	input  wire                  clk_i,
	input  wire [ADDR_WIDTH-1:0] addr_a_i,
	input  wire                  wren_a_i,
	input  wire [DATA_WIDTH-1:0] wdata_a_i,
	output wire [DATA_WIDTH-1:0] q_a_o,
	input  wire [ADDR_WIDTH-1:0] addr_b_i,
	input  wire                  wren_b_i,
	input  wire [DATA_WIDTH-1:0] wdata_b_i,
	output wire [DATA_WIDTH-1:0] q_b_o
);
	localparam NUM_WORDS = (1 << ADDR_WIDTH);

	`ifdef ALTERA_RESERVED_QIS
	altsyncram #(
		.address_reg_b                 ("CLOCK0"),
		.clock_enable_output_a         ("BYPASS"),
		.clock_enable_output_b         ("BYPASS"),
		.indata_reg_b                  ("CLOCK0"),
		.init_file                     (MEM_INIT_FILE),
		.intended_device_family        (DEVICE_FAMILY),
		.lpm_hint                      ("ENABLE_RUNTIME_MOD=NO"),
		.lpm_type                      ("altsyncram"),
		.numwords_a                    (NUM_WORDS),
		.numwords_b                    (NUM_WORDS),
		.operation_mode                ("BIDIR_DUAL_PORT"),
		.outdata_aclr_a                ("NONE"),
		.outdata_aclr_b                ("NONE"),
		.outdata_reg_a                 ("UNREGISTERED"),
		.outdata_reg_b                 ("UNREGISTERED"),
		.power_up_uninitialized        ("FALSE"),
		.ram_block_type                ("M10K"),
		.read_during_write_mode_port_a ("NEW_DATA_NO_NBE_READ"),
		.read_during_write_mode_port_b ("NEW_DATA_NO_NBE_READ"),
		.width_a                       (DATA_WIDTH),
		.width_b                       (DATA_WIDTH),
		.width_byteena_a               (1),
		.width_byteena_b               (1),
		.widthad_a                     (ADDR_WIDTH),
		.widthad_b                     (ADDR_WIDTH),
		.wrcontrol_wraddress_reg_b     ("CLOCK0")
	) u_ram (
		.address_a (addr_a_i),
		.address_b (addr_b_i),
		.clock0    (clk_i),
		.data_a    (wdata_a_i),
		.data_b    (wdata_b_i),
		.q_a       (q_a_o),
		.q_b       (q_b_o),
		.wren_a    (wren_a_i),
		.wren_b    (wren_b_i)
	);

`else
	reg [DATA_WIDTH-1:0] q_a_out;
	reg [DATA_WIDTH-1:0] q_b_out;

	(* ramstyle = "M10K, no_rw_check" *) reg [DATA_WIDTH-1:0] mem_q [0:NUM_WORDS-1];

	initial begin
		if (SIM_INIT_FILE != " ") begin
			$readmemh(SIM_INIT_FILE, mem_q);
		end
	end

	always @(posedge clk_i) begin
		if (wren_a_i) begin
			mem_q[addr_a_i] <= wdata_a_i;
		end
		if (wren_a_i) begin
			q_a_out <= wdata_a_i;
		end else begin
			q_a_out <= mem_q[addr_a_i];
		end

		if (wren_b_i) begin
			mem_q[addr_b_i] <= wdata_b_i;
		end
		if (wren_b_i) begin
			q_b_out <= wdata_b_i;
		end else begin
			q_b_out <= mem_q[addr_b_i];
		end
	end

	assign q_a_o = q_a_out;
	assign q_b_o = q_b_out;
`endif

endmodule
